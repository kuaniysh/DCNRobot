*** Settings ***
Documentation     控制交换机的关键字集合，使用规范：
...
...               ---
...               1. *打开/关闭console:*
...               - 在直接包含test case的test suite内通过关键字Open Console连接，一般放在setup内
...               - *注意: 在执行Open Console的test suite结束时，需要关闭cosole，在teardown里使用Close Console*
...               - *注意: 需要在每个直接包含测试例的test suite内使用Open Console，teardown里使用Close Console*
...               - 测试模块初始化的过程，如果需要控制Switch，需要在初始化关键字的开始使用Open Console，在初始化关键字的结束时关闭Close Console，一般初始化关键字在最高层test suite的setup中
...               - 测试模块清理配置的过程，如果需要控制Switch，需要在清理配置的关键字的开始使用Open Console，在清理配置的关键字的结束时关闭Close Console，一般清理配置的关键字在最高层test suite的teardown中
...
...               ---
...               2. *执行命令,获取结果*
...               - 一般执行命令的起始地点在enable模式，关键字Enable Mode
...               - 执行单条命令并返回命令执行后的输出，关键字 Exec Cmd，该关键字的执行起点依据上一条命令，并不是在enable模式下
...               - 一般使能某个功能，是一串命令的组合，关键字 Exec Cmd List，该关键字的执行起点在enable模式,按命令list的顺序依次配置执行，返回的是每条命令的输出信息的list
...               - 关键字 Exec Cmd List In ConfigMode, 该关键字的执行起点在config模式,按命令list的顺序依次配置执行,返回的是每条命令的输出信息的列表
...
...               ---
...               3. *Debug相关:*
...               - 打开Debug: Start Debug
...               - 获取Debug信息: Get Debug
...               - 关闭Debug: Stop Debug
...               - 获取Debug信息，正则匹配成功时退出：Get Debug Output Until Expect
...               - 获取Debug信息，直到等待时间结束： Get Debug Output Until Timeout
...
...               举例：
...               | Start Debug | s1 | debug driver |
...               | 等待固定时间 或 进行其他的操作(除对s1操作) |
...               | ${output}= | Get Debug | s1 |
...               | Stop Debug | s1 |
...               | 下面可对output内容进行判断 |
...
...               或者
...               | Start Debug | s1 | debug driver |
...               | ${output}= |Get Debug Output Until Expect | s1 | 正则表达式 |
...               | Stop Debug | s1 |
...
...               ---
...               4. *常用命令:*
...               - 重启交换机 Reload Switch
...               - 保存配置 Write Config
...               - Show Version
...               - Set Default
...               - Get Cpu Mac
...               - Get Vlan Mac
...               - Enter BootRom From Img
...               - Enter BootRom From BootRom
...
...               ---
...               5. *注意事项*
...               - 命令执行结束的标志为匹配到提示符，timeout时间为1分钟，如果命令的执行时间多于1分钟，可以参考Reload Switch关键字的实现，用Telnet.Set Timeout ${timeout}暂时修改timeout时间，然后执行完毕后再恢复默认值
...               - 在一个test suite里console的完整执行log会另存一份，当执行关键字Close Console时，会进行写入，有需要可以查询该关键字
Library           Collections
Library           String
Library           ../lib/Tools.py
Library           ../lib/MyTelnet.py    WITH NAME    Telnet

*** Variables ***
${sw_prompt}      (?m)^[-a-zA-Z0-9_]{1,30}(\\(.*\\))?([#>])|^\\[Boot\\]:|^BCM\.[0-9]>|^->|^--More--|Exit from bcm shell mode|^/ #|^\\{\\}|^login:|^Username:|^Password:|^user:|^Host IP Address:|^Server IP Address:
${sw_prompt_is_regexp}    ${True}
${sw_default_log_level}    ${None}
${sw_terminal_type}    vt100
${sw_terminal_emulation}    ${True}
${sw_timeout}     1 minute
${sw_regexp_reload}    Mac\\sAddr\\s
${sw_monitor}     ${True}
${sw_monitor_loglevel}    INFO
${sw_newline}     LF
${apc_prompt}     (?m)^User\\s+Name\\s+:|^Password\\s+:|^APC>

*** Keywords ***
Open Console
    [Arguments]    ${ip}    ${port}    ${alias}
    [Documentation]    打开console连接，一般用于测试例初始化时，建议在测试集中定于 Init Test Case关键字，其中引用Open Console，打开需要控制的console；
    ...    \ \ - 默认情况，timeout为1分钟
    ...    \ - 默认情况，会打开monitor选项，即将console的输出另存一份，以获得整个console的输出
    ...
    ...    args：
    ...    - ip \ \ \ \ ： \ \ CCM地址
    ...    - port \ ： \ CCM端口号
    ...    - alias \ ： \ \ 别名，一般在测试例集中定义
    ...
    ...    return： 无
    ...
    ...    example：
    ...    | Open Console | 172.16.11.51 | ${10001} | s1 |
    ...    | Open Console | ${s1_ip} | ${s1_port} | ${s1_alias} |
    Telnet.Open Connection    ${ip}    ${alias}    ${port}    timeout=${sw_timeout}    prompt_is_regexp=${sw_prompt_is_regexp}    prompt=${sw_prompt}
    ...    default_log_level=${sw_default_log_level}    terminal_emulation=${sw_terminal_emulation}    terminal_type=${sw_terminal_type}    monitor=${sw_monitor}    newline=${sw_newline}
    Switch.Enable Mode    ${alias}
    Switch.Set Exec TimeOut    ${alias}

Enable Mode
    [Arguments]    ${alias}
    [Documentation]    设备进入Enable模式，同时执行terminal length 0，以防止--more--
    ...
    ...    args:
    ...    - alias 别名
    Telnet.Switch Connection    ${alias}
    Telnet.Read
    Telnet.Write Bare    \x03
    Telnet.Read Until Prompt
    Telnet.Read
    Telnet.Execute Command    enable
    Telnet.Execute Command    terminal length 0

Exec Cmd
    [Arguments]    ${alias}    ${cmd}
    [Documentation]    向设备输入命令，一般情况，建议使用Exec Cmd List，这样可以更清晰的表达这组命令的意义
    ...
    ...    args：
    ...    - alias
    ...    - cmd ： 命令
    ...
    ...    return：
    ...    - output：设备对该命令的响应
    Telnet.Switch Connection    ${alias}
    Telnet.Read
    ${output}=    Telnet.Execute Command    ${cmd}
    [Return]    ${output}

Close Console
    [Arguments]    ${alias}    ${monitor_loglevel}=${sw_monitor_loglevel}    ${monitor}=${sw_monitor}
    [Documentation]    关闭console连接，一般用于test suite的teardown中
    ...
    ...    注意：Telnet实例的生存范围是test suite，即在同一个test suite里采用Open Console打开的连接是可用的，在另一个test suite中是不能引用的，故需要在每个test suite里建立setup来Open Console及在teardown中Close Console
    ...
    ...    args：
    ...    - alias \ ： \ \ 别名，一般在测试例集中定义
    ...
    ...    return： 无
    ...
    ...    example：
    ...    | Close Console | s1 |
    ...    | Close Console | ${s1_alias} |
    Telnet.Switch Connection    ${alias}
    Telnet.Set Monitor    ${monitor}
    Telnet.Close Connection    ${monitor_loglevel}

Close All Console
    [Documentation]    该关键字不能将console的全部输出记录到log里，不建议使用
    ...
    ...    Closes all open connections and empties the connection cache.
    ...
    ...    If multiple connections are opened, this keyword should be used in a test or suite teardown to make sure that all connections are closed. It is not an error is some of the connections have already been closed by `Close Connection`.
    ...
    ...    After this keyword, new indexes returned by `Open Connection` keyword are reset to 1.
    Telnet.Close All Connections

Exec Cmd Until Regexp
    [Arguments]    ${alias}    ${cmd}    ${regexp}    ${timeout}=${None}
    [Documentation]    该关键字用于执行一条命令，用正则匹配返回信息，直到匹配成功，匹配不成功会导致该关键字失败
    ...
    ...    注意：该关键字不会读取返回信息在匹配字符后面的内容，会导致后面的读取信息中包含该关键字的内容，容易出错，请慎用，如必须采用，请在该关键字后执行Telnet.Read来消耗掉整个返回信息
    ...
    ...    Exec cmd and reads output until any of the `expected` regular expressions match.
    ...
    ...    This keyword accepts any number of regular expressions patterns or compiled Python regular expression objects as arguments. Text up to and including the first match to any of the regular expressions is returned and logged. If no match is found, this keyword fails. How much to wait for the output depends on the configured timeout.
    ...
    ...    See the documentation of Python `re` module for more information about the supported regular expression syntax. Notice that possible backslashes need to be escaped in Robot Framework test data.
    ...
    ...    Examples:
    ...    | Exec Cmd Until Regexp | s1 | show version | 1.0 |
    Telnet.Switch Connection    ${alias}
    Telnet.Read
    Telnet.Write    ${cmd}
    ${timeoutFlag}=    Tools.Is None    ${timeout}
    Run Keyword If    not ${timeoutFlag}    Telnet.Set Timeout    ${timeout}
    ${output}=    Telnet.Read Until Regexp    ${regexp}
    Run Keyword If    not ${timeoutFlag}    Telnet.Set Timeout    ${sw_timeout}
    [Return]    ${output}

Exec Cmd List
    [Arguments]    ${alias}    @{cmd_list}
    [Documentation]    KeyWord: Exec Cmd List
    ...
    ...    \ exec command list based on enable mode
    ...
    ...    args:
    ...
    ...    - ${alias}: alias name of console,built on keyword of Open Console
    ...    - @{cmd_list}: list of cmd string
    ...
    ...    example:
    ...    | Exec Cmd List | ${s1_alias} | show version |
    ...    | Exec Cmd List | ${s1_alias} | config | interface ${s1p1} | shutdown |
    Switch.Enable Mode    ${alias}
    ${output_list}    Create List
    : FOR    ${cmd}    IN    @{cmd_list}
    \    ${out}=    Telnet.Execute Command    ${cmd}
    \    Collections.Append To List    ${output_list}    ${out}
    Log List    ${output_list}
    [Return]    ${output_list}

Exec Cmd List In ConfigMode
    [Arguments]    ${alias}    @{cmd_list}
    [Documentation]    KeyWord: Exec Cmd List In ConfigMode
    ...
    ...    \ exec command list based on config mode
    ...
    ...    args:
    ...
    ...    - ${alias}: alias name of console,built on keyword of Open Console
    ...    - @{cmd_list}: list of cmd string
    ...
    ...    example:
    ...    | Exec Cmd List In ConfigMode | ${s1_alias} | interface ${s1p1} | shutdown |
    Switch.Enable Mode    ${alias}
    Telnet.Execute Command    config
    ${output_list}    Create List
    : FOR    ${cmd}    IN    @{cmd_list}
    \    ${out}=    Telnet.Execute Command    ${cmd}
    \    Collections.Append To List    ${output_list}    ${out}
    Log List    ${output_list}

Reload Switch
    [Arguments]    ${alias}    ${write}=${True}    ${wait}=60    ${regexp_reload}=${sw_regexp_reload}    ${timeout}=20 minutes
    [Documentation]    重启设备
    ...
    ...    args：
    ...    - ${alias}：Open Console时设置的别名
    ...    - ${write}: 是否保存配置，默认为True
    ...    - ${wait}: \ 匹配到重启成功字符后的等待时间，默认为60s，一般不用修改
    ...    - ${regexp_reload}: 重启完成的匹配字符，默认为Mac\\sAddr\\s
    ...    - ${timeout}: 最长等待时间，默认为20分钟，超过会报错
    Run Keyword If    ${write}    Switch.Write Config    ${alias}
    Run Keyword Unless    ${write}    Switch.Enable Mode    ${alias}
    Telnet.Write    reload
    Telnet.Read Until Regexp    \\[Y/N\\]
    Telnet.Write    Y
    Telnet.Set Timeout    ${timeout}
    Telnet.Read Until Regexp    ${regexp_reload}
    Telnet.Set Timeout    ${sw_timeout}
    Sleep    ${wait}
    Telnet.Read
    Switch.Enable Mode    ${alias}
    Switch.Set Exec TimeOut    ${alias}

Write Config
    [Arguments]    ${alias}
    [Documentation]    保存AC的配置
    ...
    ...    args：
    ...    - ${alias}：Open Console时设置的别名
    Switch.Enable Mode    ${alias}
    Telnet.Write    write
    ${output}=    Telnet.Read Until Regexp    \\[Y/N\\]:    [-a-zA-Z0-9_. ]{1,30}(\\(.*\\))?#
    ${writeFlag}=    Tools.Is Match From String    ${output}    \\[Y/N\\]:
    Run Keyword If    ${writeFlag}    Telnet.Write    Y
    Run Keyword If    ${writeFlag}    Telnet.Read Until Prompt
    Run Keyword Unless    ${writeFlag}    Telnet.Read

Set Default
    [Arguments]    ${alias}
    [Documentation]    set default switch
    ...
    ...    args:
    ...    - ${alias} ailas name of the console connection, built on keyWord of Open Console
    ...
    ...    example:
    ...    | Set Default | ${s1_alias} |
    Switch.Enable Mode    ${alias}
    Telnet.Write    set default
    Telnet.Read Until Regexp    \\[Y/N\\]    [-a-zA-Z0-9_. ]{1,30}(\\(.*\\))?#
    Telnet.Write    Y
    Telnet.Read Until Prompt

Show Version
    [Arguments]    ${alias}
    [Documentation]    获取AC的版本信息
    ...
    ...    args：
    ...    - ${alias}：Open Console时设置的别名
    Switch.Enable Mode    ${alias}
    ${output}=    Telnet.Execute Command    show version
    Log    ${output}
    [Return]    ${output}

Set Exec TimeOut
    [Arguments]    ${alias}    ${timeout}=0
    [Documentation]    设置AC的exec-timeout
    ...
    ...    args：
    ...    - ${alias}：Open Console时设置的别名
    ...    - ${timeout}: 默认为0
    Switch.Exec Cmd List    ${alias}    config    exec-timeout ${timeout}

Set WatchDog Disable
    [Arguments]    ${alias}
    [Documentation]    关闭AC的看门狗
    ...
    ...    args：
    ...    - ${alias}：Open Console时设置的别名
    Switch.Exec Cmd List    ${alias}    config    watchdog disable

Get Cpu Mac
    [Arguments]    ${alias}    ${input}=show interface detail | include address is
    [Documentation]    获取AC的Cpu mac
    ...
    ...    args：
    ...    - ${alias}：Open Console时设置的别名
    ...    - ${input}: 获取mac输入的命令 默认为show interface detail | include address is
    Switch.Enable Mode    ${alias}
    ${output}=    Telnet.Execute Command    ${input}
    ${cpuMac}=    Tools.Get Value From String    ${output}    .*?,\\s+address is\\s(.*?)\\s
    Should Not Be Equal    ${cpuMac}    ${None}
    [Return]    ${cpuMac}

Get Vlan Mac
    [Arguments]    ${alias}
    [Documentation]    获取AC的vlan mac
    ...
    ...    args：
    ...    - ${alias}：Open Console时设置的别名
    Switch.Enable Mode    ${alias}
    ${output}=    Telnet.Execute Command    show interface vlan 1
    ${isIntVlan1}=    Tools.Is Match From String    ${output}    .*? doesn't exist or error
    Run Keyword If    ${isIntVlan1} == ${True}    Switch.Exec Cmd List In ConfigMode    ${alias}    interface vlan 1
    Run Keyword If    ${isIntVlan1} == ${True}    Sleep    2
    ${output}=    Telnet.Execute Command    show interface vlan 1
    ${vlanMac}=    Tools.Get Value From String    ${output}    .*?,\\s+address is\\s(.*?)\\s
    Should Not Be Equal    ${vlanMac}    ${None}
    Run Keyword If    ${isIntVlan1} == ${True}    Switch.Exec Cmd List In ConfigMode    ${alias}    no interface vlan 1
    [Return]    ${vlanMac}

Enter BootRom From Img
    [Arguments]    ${alias}    ${regexp_testram}=Testing RAM    ${regexp_bootrom}=\\[Boot\\]    ${timeout}=3 minutes
    [Documentation]    从img进入bootrom
    ...
    ...    args：
    ...    - ${alias}：Open Console时设置的别名
    ...    - ${regexp_testram}: 内存检测的匹配字符，默认为Testing RAM
    ...    - ${regexp_bootrom}: 进入bootrom的匹配字符，默认为[Boot]
    ...    - ${timeout}: 最长等待时间，默认为3分钟，超过会报错
    Switch.Enable Mode    ${alias}
    Telnet.Write    reload
    Telnet.Read Until Regexp    \\[Y/N\\]
    Telnet.Write    Y
    Telnet.Set Timeout    ${timeout}
    Telnet.Read Until Regexp    ${regexp_testram}
    Telnet.Write    \x02
    Telnet.Read Until Regexp    ${regexp_bootrom}
    Telnet.Set Timeout    ${sw_timeout}
    Telnet.Read

Enter BootRom From BootRom
    [Arguments]    ${alias}    ${regexp_testram}=Testing RAM    ${regexp_bootrom}=\\[Boot\\]    ${timeout}=3 minutes
    [Documentation]    从bootrom进入bootrom
    ...
    ...    args：
    ...    - ${alias}：Open Console时设置的别名
    ...    - ${regexp_testram}: 内存检测的匹配字符，默认为Testing RAM
    ...    - ${regexp_bootrom}: 进入bootrom的匹配字符，默认为[Boot]:
    ...    - ${timeout}: 最长等待时间，默认为3分钟，超过会报错
    Telnet.Switch Connection    ${alias}
    Telnet.Read
    Telnet.Write    reboot
    Telnet.Set Timeout    ${timeout}
    Telnet.Read Until Regexp    ${regexp_testram}
    Telnet.Write    \x02
    Telnet.Read Until Regexp    ${regexp_bootrom}
    Telnet.Set Timeout    ${sw_timeout}
    Telnet.Read

Get Debug Output Until Expect
    [Arguments]    ${alias}    ${expect}    ${timeout}=${180}
    [Documentation]    在debug信息中查找expect信息，并返回信息，注意如果没有expect信息，在timeout到时候会返回全部信息
    ...
    ...    参数：
    ...    - alias：设备console别名
    ...    - expect： 要查找的信息
    ...    - timeout：默认180s，注意单位为s
    ...
    ...    返回：是否查找成功，输出信息
    ...
    ...    用法：
    ...    - 先使用关键字Start Debug
    ...    - 然后Get Debug Output Until Expect
    ...    - 最后关闭debug： Stop Debug
    Telnet.Switch Connection    ${alias}
    Telnet.Set Timeout    ${timeout}
    @{res}=    Telnet.Debug Read Until    ${expect}
    Telnet.Set Timeout    ${sw_timeout}
    Log    ${res}
    [Return]    @{res}

Stop Debug
    [Arguments]    ${alias}    ${cmd}=\x0e
    [Documentation]    关闭 debug
    ...
    ...    参数：
    ...    - alias：设备console别名
    ...    - cmd： 默认为ctrl-o
    Telnet.Switch Connection    ${alias}
    Telnet.Write    ${cmd}
    Telnet.Read Until Prompt
    Telnet.Read

Start Debug
    [Arguments]    ${alias}    ${cmd}
    [Documentation]    打开debug
    ...
    ...    参数：
    ...    - alias：设备console别名
    ...    - cmd：debug命令
    Telnet.Switch Connection    ${alias}
    Telnet.Write    ${cmd}

Get Debug Output Until Timeout
    [Arguments]    ${alias}    ${timeout}
    [Documentation]    获取timeout时间内的debug信息
    ...
    ...    参数：
    ...    - alias：设备console别名
    ...    - timeout： \ 注意单位为s
    ...
    ...    返回：timeout时间内的输出信息
    ...
    ...    用法：
    ...    - 先使用关键字Start Debug
    ...    - 然后Get Debug Output Until Timeout
    ...    - 最后关闭debug： Stop Debug
    Telnet.Switch Connection    ${alias}
    Sleep    ${timeout}
    ${output}=    Telnet.My Read Eager
    Log    ${output}
    [Return]    ${output}

Get Debug
    [Arguments]    ${alias}
    [Documentation]    获取debug信息,该关键字会读取console的内容并立刻返回
    ...
    ...    参数：
    ...    - alias：设备console别名
    ...
    ...    返回：debug输出信息
    ...
    ...    用法：
    ...    - 先使用关键字Start Debug
    ...    - 然后可以进行其他操作
    ...    - Get Debug，获取第2步操作过程中的debug信息
    ...    - 最后关闭debug： Stop Debug
    Telnet.Switch Connection    ${alias}
    ${output}=    Telnet.My Read Eager
    Log    ${output}

Exec Cmd Until Timeout
    [Arguments]    ${alias}    ${cmd}    ${timeout}=${None}
    [Documentation]    该关键字会等待timeout的时间，获取执行命令的返回值，由于没有判断提示符，不能确定命令的输出信息是否完全返回，请慎用
    Telnet.Switch Connection    ${alias}
    Telnet.Read
    Telnet.Write    ${cmd}
    ${timeoutFlag}=    Tools.Is None    ${timeout}
    Run Keyword If    not ${timeoutFlag}    Sleep    ${timeout}
    ${output}=    Telnet.Read
    [Return]    ${output}

APC ON
    [Arguments]    ${alias}    ${id}    ${user}=apc    ${pwd}=apc -c
    Switch.APC Login    ${alias}    ${user}    ${pwd}
    Telnet.Read
    ${output}=    Telnet.Execute Command    on ${id}
    Switch.APC Logout    ${alias}

Open APC
    [Arguments]    ${ip}    ${port}    ${alias}
    Telnet.Open Connection    ${ip}    ${alias}    ${port}    timeout=1 minute    prompt_is_regexp=${True}    prompt=${apc_prompt}
    ...    default_log_level=${None}    terminal_emulation=${False}    monitor=${sw_monitor}    newline=CR

Close APC
    [Arguments]    ${alias}    ${monitor_loglevel}=${sw_monitor_loglevel}    ${monitor}=${sw_monitor}
    Telnet.Switch Connection    ${alias}
    Telnet.Set Monitor    ${monitor}
    Telnet.Close Connection    ${monitor_loglevel}

APC Login
    [Arguments]    ${alias}    ${username}=apc    ${password}=apc-c
    Telnet.Switch Connection    ${alias}
    Telnet.Read
    Telnet.Write Bare    \r
    Telnet.Write Bare    \r
    Telnet.Write    ${username}
    Telnet.Read Until Regexp    Password\\s+:
    Telnet.Write    ${password}
    Telnet.Read Until Prompt

APC Logout
    [Arguments]    ${alias}
    Telnet.Switch Connection    ${alias}
    Telnet.Read
    Telnet.Write    exit
    Telnet.Read Until Regexp    Bye.

APC OFF
    [Arguments]    ${alias}    ${id}    ${user}=apc    ${pwd}=apc-c
    Switch.APC Login    ${alias}    ${user}    ${pwd}
    Telnet.Read
    ${output}=    Telnet.Execute Command    off ${id}
    Switch.APC Logout    ${alias}
