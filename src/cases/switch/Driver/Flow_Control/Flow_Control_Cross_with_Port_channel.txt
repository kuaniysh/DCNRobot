*** Settings ***
Documentation     01 与port-channel交叉,在port-channel下,功能正确生效。
...               - s1p1与s1p2开启流控,并将s1p1与s1p2加入port-group
...               - 验证port-channel模式下,flow control功能生效
...
...               02 Port-channel下以及成员端口下不能配置，加入时若配置不一样，无法加入。
Suite Setup       init suite test
Suite Teardown    uninit suite test
Resource          resource_flow_control.txt

*** Test Cases ***
1.2.4.4.1 在port-channel模式下，功能正常生效
    [Setup]    01_init
    #
    log Step Start    Step 1    s1p1与s1p2开启流控,并将s1p1与s1p2加入port-group
    set flow control    ${s1_alias}    ${s1p1}
    set flow control    ${s1_alias}    ${s1p2}
    Ixiasend.Set Port Flowcontrol    @{testerp1}    1
    Ixiasend.Set Port Flowcontrol    @{testerp2}    1
    sleep    5s    等待s1p1与s1p2 UP
    Exec Cmd List In ConfigMode    ${s1_alias}    interface ${s1p3}    loopback
    Exec Cmd List In ConfigMode    ${s1_alias}    port-group 1
    Set Port Channel Member    ${s1_alias}    ${s1p1}    1    on
    Set Port Channel Member    ${s1_alias}    ${s1p2}    1    on
    log Step End    Step 1
    #
    #
    log Step Start    Step 2    验证port-channel模式下,flow control功能生效
    exec cmd list    ${s1_alias}    clear counters
    Ixiasend.Clear Statics    @{testerp1}
    Ixiasend.Clear Statics    @{testerp2}
    Ixiasend.Start Transmit    @{testerp1}
    Ixiasend.Start Transmit    @{testerp2}
    sleep    10s
    Ixiasend.Stop Transmit    @{testerp1}
    Ixiasend.Stop Transmit    @{testerp2}
    ${tp1_tx}    ${tp1_rx}    ${tp1_FCR}=    Get Statics    @{testerp1}    txpackets    rxpackets
    ...    flowControlFrames
    ${tp2_tx}    ${tp2_rx}    ${tp2_FCR}=    Get Statics    @{testerp2}    txpackets    rxpackets
    ...    flowControlFrames
    ${p1_tx}=    Get Show Interface Port    ${s1_alias}    ${s1p1}    tx_pause
    ${p2_tx}=    Get Show Interface Port    ${s1_alias}    ${s1p2}    tx_pause
    ${p1_pause}=    Get From Dictionary    ${p1_tx}    tx_pause
    ${p2_pause}=    Get From Dictionary    ${p2_tx}    tx_pause
    should be true    (${p1_pause}>${0}) and (${p2_pause}>${0}) and ${tp1_FCR}>${0} and ${tp2_FCR}>${0}    s1p1与s1p2发送pause帧
    ${res}=    check more or less    ${${tp1_rx}+${tp2_rx}}    ${${tp1_tx}+${tp2_tx}+${tp1_FCR}+${tp2_FCR}}    0.05
    should be true    ${res}    tp1与tp2发送速率受流控影响,总速率下降到与tp1+tp2收包速率一致.
    log Step End    Step 2
    #
    #
    [Teardown]    01_uninit

1.2.4.4.2 port-channel及成员端口下不能修改相关配置,成员端口配置不一样时,不能加入port-channel
    [Setup]    02_init
    log Step Start    Step 1
    Check Cross with Port Channel type 3    ${s1_alias}    ${CMD_FLOW_CONTROL}    no ${CMD_FLOW_CONTROL}    ${CMD_FLOW_CONTROL}    ${s1p1}    ${s1p2}
    log Step End    Step 1
    #
    #
    [Teardown]    02_uninit

*** Keywords ***
01_init
    tools.comment    ${SUITE_NAME}    ${TEST_NAME}    配置拓扑>>>
    tools.comment    ${SUITE_NAME}    ${TEST_NAME}    配置完成<<<

01_uninit
    tools.comment    ${SUITE_NAME}    ${TEST_NAME}    恢复配置>>>>>
    Exec Cmd List In ConfigMode    ${s1_alias}    no port-group 1
    Exec Cmd List In ConfigMode    ${s1_alias}    interface ${s1p3}    no loopback
    unset Flow Control    ${s1_alias}    ${s1p1}
    unset Flow Control    ${s1_alias}    ${s1p2}
    Ixiasend.Set Port Config Default    @{testerp1}
    Ixiasend.Set Port Config Default    @{testerp2}
    sleep    5s    等待端口up
    tools.comment    ${SUITE_NAME}    ${TEST_NAME}    恢复配置完成<<<<<

02_init
    tools.comment    ${SUITE_NAME}    ${TEST_NAME}    配置拓扑>>>
    tools.comment    ${SUITE_NAME}    ${TEST_NAME}    配置完成<<<

02_uninit
    tools.comment    ${SUITE_NAME}    ${TEST_NAME}    恢复配置>>>>>
    Exec Cmd List In ConfigMode    ${s1_alias}    no port-group 1
    unset Flow Control    ${s1_alias}    ${s1p1}
    unset Flow Control    ${s1_alias}    ${s1p2}
    sleep    5s    wait for port up
    tools.comment    ${SUITE_NAME}    ${TEST_NAME}    恢复配置完成<<<<<
